plugins {
    id 'java'
    id 'org.jetbrains.kotlin.jvm' version '1.9.0-RC'
    id "com.github.johnrengelman.shadow" version "7.1.2"
}

group = 'de'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
    maven {
        name = "CodeMC"
        url = uri("https://repo.codemc.io/repository/maven-public/")
    }
}

dependencies {
    compileOnly "io.papermc.paper:paper-api:1.20.1-R0.1-SNAPSHOT"
    shadow "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    implementation "de.tr7zw:item-nbt-api:2.11.3"
}

shadowJar {
    relocate("de.tr7zw.changeme.nbtapi", "de.staticred.kia.nbtapi")
}

build {
    dependsOn(shadowJar)
}

def targetJavaVersion = 20
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release = targetJavaVersion
    }
}


processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('paper-plugin.yml') {
        expand props
    }
}

kotlin {
    jvmToolchain(20)
}
